plugins {
    id 'org.asciidoctor.jvm.convert' version "3.3.2"
}

configurations {
    asciidoctorExtensions
}

dependencies {
    implementation project(':mentors-commons:common-exception')
    implementation project(':mentors-commons:common-domain')
    implementation project(':mentors-image')
    implementation project(':mentors-domain')
    testImplementation testFixtures(project(':mentors-domain'))
    testImplementation testFixtures(project(':mentors-infrastruct:persistence-database'))


    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    configurations 'asciidoctorExtensions'
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir
    dependsOn test
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor

    from "${asciidoctor.outputDir}"
    into file("src/main/resources/static/docs")
}

bootJar {
    dependsOn copyDocument
    enabled = true
}

jar {
    enabled false
}
